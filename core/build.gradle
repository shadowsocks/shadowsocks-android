apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.sdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            ndkBuild {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                arguments "-j${Runtime.runtime.availableProcessors()}"
            }
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions.jvmTarget = javaVersion

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

androidExtensions {
    experimental = true
}

def coroutinesVersion = '1.3.1'
def roomVersion = '2.1.0'
def workVersion = '2.2.0'
dependencies {
    api project(':plugin')
    api "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    api "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    api "androidx.lifecycle:lifecycle-livedata-core-ktx:$lifecycleVersion"
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    api 'androidx.preference:preference:1.1.0'
    api "androidx.room:room-runtime:$roomVersion"
    api "androidx.work:work-runtime-ktx:$workVersion"
    api "androidx.work:work-gcm:$workVersion"
    api 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    api 'com.google.code.gson:gson:2.8.5'
    api 'com.google.firebase:firebase-analytics:17.2.0'
    api 'com.google.firebase:firebase-config:19.0.1'
    api 'dnsjava:dnsjava:2.1.9'
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutinesVersion"
    api 'org.connectbot.jsocks:jsocks:1.0.0'
    kapt "androidx.room:room-compiler:$roomVersion"
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidEspressoVersion"
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.1-beta01'
}
