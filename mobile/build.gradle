import com.android.build.OutputFile
import java.util.regex.Matcher
import java.util.regex.Pattern

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def getCurrentFlavor() {
    String task = getGradle().getStartParameter().getTaskRequests().toString()
    Matcher matcher = Pattern.compile("(assemble|generate)\\w*(Release|Debug)").matcher(task)
    if (matcher.find()) return matcher.group(2).toLowerCase() else {
        println "Warning: No match found for $task"
        return "debug"
    }
}

android {
    buildToolsVersion rootProject.buildToolsVersion
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "com.github.shadowsocks"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.sdkVersion
        versionCode 4060100
        versionName "4.6.1"
        testApplicationId "com.github.shadowsocks.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "fa", "fr", "ja", "ko", "ru", "zh-rCN", "zh-rTW"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            multiDexEnabled true
            pseudoLocalesEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions.checkReleaseBuilds false
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86'
            universalApk true
        }
    }
    sourceSets.main.jniLibs.srcDirs +=
            new File(project(':core').buildDir, "intermediates/bundles/${getCurrentFlavor()}/jni")
    sourceSets.main.jniLibs.srcDirs += new File(project(':core').projectDir, "src/overture/bin")
}

dependencies {
    implementation project(':core')
    implementation "android.arch.work:work-runtime-ktx:$workVersion"
    implementation "androidx.browser:browser:$androidxVersion"
    implementation "androidx.gridlayout:gridlayout:$androidxVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "com.google.android.material:material:$androidxVersion"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'
    implementation 'com.google.android.gms:play-services-vision:15.0.2'
    implementation 'com.google.firebase:firebase-ads:15.0.1'
    implementation 'com.google.firebase:firebase-config:16.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation "com.takisoft.preferencex:preferencex-simplemenu:$preferencexVersion"
    implementation 'com.twofortyfouram:android-plugin-api-for-locale:1.0.4'
    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'
    implementation 'net.glxn.qrgen:android:2.0'
    implementation 'xyz.belvi.mobilevision:barcodescanner:2.0.3'
    kapt "androidx.room:room-compiler:$roomVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "android.arch.work:work-testing:$workVersion"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidEspressoVersion"
}
repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'

ext.abiCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, x86: 3]
if (getCurrentFlavor() == 'release') android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def offset = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        if (offset != null) output.versionCodeOverride = variant.versionCode + offset
    }
}
